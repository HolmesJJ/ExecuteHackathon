apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'de.undercouch.download'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.0"
    defaultConfig {
        applicationId "com.example.enactusapp"
        minSdkVersion 23
        // UVCCamera需要27以下
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        sourceSets.main {
            jniLibs.srcDir 'src/main/libs' //这一句是设置目标的so存放路径，也就是组装到apk中的so路径
            jni.srcDirs = [] //这一句是禁用gradle默认的ndk-build，防止AS自己生成android.mk编译
        }
        ndk {
            abiFilters "arm64-v8a", "armeabi-v7a"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        release {
            storeFile file('../keystore/release.jks')
            storePassword 'enactus'
            keyAlias 'enactus'
            keyPassword 'enactus'
        }
        debug {
            storeFile file('../keystore/debug.keystore')
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }
    aaptOptions {
        noCompress "tflite"
    }
    lintOptions {
        abortOnError false
    }
}

// import DownloadModels task
project.ext.ASSET_DIR = projectDir.toString() + '/src/main/assets'
project.ext.TMP_DIR   = project.buildDir.toString() + '/downloads'

// Download default models; if you wish to use your own models then
// place them in the "assets" directory and comment out this line.
//apply from: "download_model.gradle"

apply from:'download_model.gradle'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // OpenCV
    implementation fileTree(dir: "$buildDir/native-libs", include: 'native-libs.jar')
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'
    // permissions
    implementation 'pub.devrel:easypermissions:3.0.0'
    // TTS sdk依赖于 okhttp 必须外部依赖或导入，否则报错
    implementation 'com.squareup.okhttp3:okhttp:3.12.12'
    // Fragmentation https://github.com/YoKeyword/Fragmentation
    implementation 'me.yokeyword:fragmentationx:1.0.2'
    implementation 'me.yokeyword:fragmentationx-swipeback:1.0.2'
    implementation 'me.yokeyword:eventbus-activity-scope:1.1.0'
    // EventBus https://github.com/greenrobot/EventBus
    implementation 'org.greenrobot:eventbus:3.1.1'
    // FireBase
    implementation 'com.google.firebase:firebase-messaging:17.3.4'
    // Glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    implementation project(path: ':bluetoothlibrary')
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'
    // TTS sdk依赖于 okhttp 必须外部依赖或导入，否则报错
    implementation 'com.squareup.okhttp3:okhttp:3.12.12'
    // TensorFlow
    implementation 'org.tensorflow:tensorflow-lite:0.0.0-nightly'
    // USB Camera
    implementation 'com.github.jiangdongguo:AndroidUSBCamera:2.3.4'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    // OpenCV
    implementation project(path: ':openCVLibrary340')
    // Gaze
    implementation project(":libgaze-release")
    implementation project(':gazetracker-release')
}

// OpenCV
task nativeLibsToJar(type: Jar, description: 'create a jar archive of the native libs') {
    destinationDir file("$buildDir/native-libs")
    baseName 'native-libs'
    extension 'jar'
    from fileTree(dir: 'libs', include: '**/*.so')
    into 'lib/'
}

tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn(nativeLibsToJar)
}
